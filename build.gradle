plugins {
    id "com.diffplug.spotless" version "7.2.1"
    id "org.openrewrite.rewrite" version "7.14.0"
}

allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    tasks.register('view_paths') {
        doLast { task ->
            println "------------------"
            println "$task.project.name"
            println "------------------"
        }
    }

    dependencies {
        // Prod
        // Test
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.13.4'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.13.4'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.19.0'
        testImplementation group: 'net.datafaker', name: 'datafaker', version: '2.4.4'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

subprojects {
    group = "com.comparitiko.${project.name}"
    version = '0.0.1'

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21


    apply plugin: 'java'
    apply plugin: "com.diffplug.spotless"
    apply plugin: "org.openrewrite.rewrite"

    sourceSets {
        main {
            java { srcDirs = ['src/main'] }
            resources { srcDirs = ['src/main/resources'] }
        }
        test {
            java { srcDirs = ['src/test'] }
            resources { srcDirs = ['src/test/resources'] }
        }
    }

    rewrite {
        activeRecipe(
            'org.openrewrite.java.OrderImports',
            'org.openrewrite.java.RemoveObjectsIsNull'
        )
    }

    spotless {
        java {
            googleJavaFormat()
            target 'src/**/*.java'
        }
    }

    dependencies {
        if (project.name != "shared") {
            implementation project(":shared")
        }
    }
}
