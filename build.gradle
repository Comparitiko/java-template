plugins {
    id "com.diffplug.spotless" version "8.0.0"
    id "org.openrewrite.rewrite" version "7.17.0"
    id 'java'
}

allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    tasks.register('view_paths') {
        doLast { task ->
            println "------------------"
            println "$task.project.name"
            println "------------------"
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: "com.diffplug.spotless"
    apply plugin: "org.openrewrite.rewrite"

    group = "com.comparitiko.${project.name}"
    version = '0.0.1'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    sourceSets {
        main {
            java { srcDirs = ['src/main'] }
            resources { srcDirs = ['src/main/resources'] }
        }
        test {
            java { srcDirs = ['src/test'] }
            resources { srcDirs = ['src/test/resources'] }
        }
    }

    rewrite {
        activeRecipe(
            'org.openrewrite.java.OrderImports',
            'org.openrewrite.java.RemoveObjectsIsNull'
        )
    }

    spotless {
        java {
            googleJavaFormat()
            target 'src/**/*.java'
        }
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        if (project.name != "shared") {
            implementation project(":shared")
        }
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '6.0.0'
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '6.0.0'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.20.0'
        testImplementation group: 'net.datafaker', name: 'datafaker', version: '2.5.1'
    }
}
